#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//--------------------- Трубач бэнда

#define NAME                            SCRIPT_YCBandB
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      20 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    0  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeFstDialog2;
procedure NodeFstDialog3;
procedure NodeFstDialog4;
procedure NodeFstDialog5;
procedure NodePartyAdd;
procedure NodePartyDel;
procedure NodeSndDialog;
procedure NodeSndDialog2;

//---------------------

//#define LVAR_Flag_A                     (6) // отвечает за ...

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       if (self_TEAM == TEAM_PLAYER) then begin
       add_timer_event(self_obj,3,3);
       end
   end
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (global_var(GVAR_DAYGLOW_MARIACHI_A) != 1) then begin
       timer_move_to_home
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
   if (fixed_param == 3) then begin
       if (cur_map_index == MAP_DAYGLOW_START) then begin
          floater(self_obj,110,COLOR_MSG_NORMAL);
       end
       if (cur_map_index == MAP_DAYGLOW_CITYHEART) then begin
          floater(self_obj,111,COLOR_MSG_NORMAL);
       end
       if (cur_map_index == MAP_DAYGLOW_BONITA_DUM) then begin
          if (global_var(GVAR_DAYGLOW_TURBINA) >= 5) then begin
          floater(self_obj,112,COLOR_MSG_NORMAL);
          end
          else begin
          floater(self_obj,113,COLOR_MSG_NORMAL);
          end
       end
       if (cur_map_index == MAP_DAYGLOW_PATRUL) then begin
          if (self_elevation == 0) then begin
          floater(self_obj,114,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 1) then begin
          floater(self_obj,115,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 2) then begin
          floater(self_obj,116,COLOR_MSG_NORMAL);
          end
       end
       if (cur_map_index == MAP_DAYGLOW_CHULAVISTA) then begin
          if (self_elevation == 0) then begin
          floater(self_obj,117,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 2) then begin
          floater(self_obj,118,COLOR_MSG_NORMAL);
          end
       end
       if (cur_map_index == MAP_DAYGLOW_NATIONALCITY) then begin
          if (self_elevation == 0) then begin
          floater(self_obj,119,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 1) then begin
          //floater(self_obj,120,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 2) then begin
          floater(self_obj,121,COLOR_MSG_NORMAL);
          end
       end
       if (cur_map_index == MAP_DAYGLOW_PIRS) then begin
          if (self_elevation == 0) then begin
          floater(self_obj,122,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 1) then begin
          floater(self_obj,123,COLOR_MSG_NORMAL);
          end
          if (self_elevation == 2) then begin
          floater(self_obj,124,COLOR_MSG_NORMAL);
          end
       end

   end
end

procedure critter_p_proc begin
   set_critter_options(1,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
   if (global_var(GVAR_DAYGLOW_MARIACHI_A) == 1) then begin
   partyRunToDude
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   set_global_var(GVAR_DAYGLOW_MARIACHI_A, -1);
   if (cur_map_index == MAP_DAYGLOW_CAMP_OLD_TOWN) then begin
      if (self_elevation == 0) then begin
      setQuest(GVAR_DAYGLOW_QST_MARIACHI, qStop);
      end
   end
   kill_critter_opt
end

procedure talk_p_proc begin
   setReaction(1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,75) // поправка к отношению за принадлежность к фракции

   if (global_var(GVAR_DAYGLOW_MARIACHI_A) >= 2) then begin
      if (getQuest(GVAR_DAYGLOW_QST_MARIACHI) <= qStop) then begin
      floater(self_obj, 104, COLOR_MSG_NORMAL);
      end
      else begin
      z_floater(self_obj, 105, COLOR_MSG_NORMAL);
      end
   end
   else if (global_var(GVAR_DAYGLOW_MARIACHI_A) == 1) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else if (global_var(GVAR_DAYGLOW_MARIACHI_A) == 0) then begin
      if (getQuest(GVAR_DAYGLOW_QST_MARIACHI) == qEnd) then begin
      dialog_proc(0,0,NodeStartDialog,-1,-1)
      end
      else if (global_var(GVAR_DAYGLOW_TURBINA) >= 5) then begin
      floater(self_obj, 103, COLOR_MSG_NORMAL);
      end
      else begin
      floater(self_obj, 102, COLOR_MSG_NORMAL);
      end
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (global_var(GVAR_DAYGLOW_MARIACHI_A) == 0) then begin
      if (cur_map_index != MAP_DAYGLOW_BONITA_DUM) then begin
      call NodeFstDialog5;
      end
      else begin
      call NodeFstDialog;
      end
   end
   else begin
      if (cur_map_index == MAP_DAYGLOW_CAMP_OLD_TOWN) then begin
      call NodeSndDialog2;
      end
      else begin
      call NodeSndDialog;
      end
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   zReply(200)
   NAnswer(202,1,NodeFstDialog2)
   NAnswer(204,1,NodeFstDialog3)
   NAnswer(201,1,Node999)
end

procedure NodeFstDialog2 begin
   Reply(203)
   NAnswer(204,1,NodeFstDialog3)
   if (getQuest(GVAR_DAYGLOW_QST_MARIACHI) == qEnd) then begin
   NAnswer(206,1,NodeFstDialog4)
   end
   NAnswer(201,1,Node999)
end

procedure NodeFstDialog3 begin
   Reply(205)
   NAnswer(202,1,NodeFstDialog2)
   if (getQuest(GVAR_DAYGLOW_QST_MARIACHI) == qEnd) then begin
   NAnswer(206,1,NodeFstDialog4)
   end
   NAnswer(201,1,Node999)
end

procedure NodeFstDialog4 begin
   Reply(207)
   NAnswer(209,1,NodePartyAdd)
   NAnswer(208,1,Node999)
end

procedure NodeFstDialog5 begin
   Reply(212)
   NAnswer(213,1,NodePartyAdd)
   NAnswer(214,1,Node999)
end

procedure NodePartyAdd begin
   set_global_var(GVAR_DAYGLOW_MARIACHI_A, 1);
   rm_timer_event(self_obj);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,TEAM_PLAYER);
   party_add(self_obj);
   Reply(210)
   NAnswer(211,1,Node999)
end

procedure NodePartyDel begin
   set_global_var(GVAR_DAYGLOW_MARIACHI_A, 0);
   set_local_var(LVAR_Home_Tile, self_tile);
   if (cur_map_index == MAP_DAYGLOW_CAMP_OLD_TOWN) then begin
   set_global_var(GVAR_DAYGLOW_MARIACHI_A, 2);
   set_local_var(LVAR_Home_Tile, 10698);
   move_to(self_obj, HomeTile, 0);
   set_local_var(LVAR_Home_Rotat, 2);
   anim(self_obj, ANIMATE_ROTATION, local_var(LVAR_Home_Rotat));
   critter_uninjure(dude_obj, (DAM_BLIND bwor DAM_CRIP_LEG_LEFT bwor DAM_CRIP_LEG_RIGHT bwor DAM_CRIP_ARM_LEFT bwor DAM_CRIP_ARM_RIGHT));
   end
   party_remove(self_obj);
   critter_add_trait(self_obj,TRAIT_OBJECT,OBJECT_TEAM_NUM,NUM_TEAM);
   call Node999;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(215)
   if (global_var(GVAR_DAYGLOW_MARIACHI_A) == 0) then begin
   NAnswer(217,1,NodePartyAdd)
   end
   if (global_var(GVAR_DAYGLOW_MARIACHI_A) == 1) then begin
   NAnswer(218,1,NodePartyDel)
   end
   NAnswer(216,1,Node999)
end

procedure NodeSndDialog2 begin
   zReply(219)
   NAnswer(220,1,NodePartyDel)
end
