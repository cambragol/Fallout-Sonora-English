#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCCafeB
#define TOWN_REP_VAR                    GVAR_TOWN_REP_DAYGLOW
#define BOX_TILE                        10  // номер тайла с личным товарным ящиком (-1, если не надо; не использовать значения <10; не может совпадать с чужими ящиками на одной локации)

#define NUM_TEAM                        TEAM_DAYGLOW_GHOUL    // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          -1                    // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_GOOD       // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_CRITICAL     // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_NEITRAL, REP_KILL_BAD, REP_KILL_CHILD, REP_KILL_CRITICAL)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог для навыка кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - отказывается
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - отказывается

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure setBarterA;
procedure setBarterB;
procedure look_at_p_proc;
procedure description_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeFstDialog;
procedure NodeSndDialog;
procedure NodeBarter;
procedure NodeBarterRoom;
procedure NodeBarterRoom2;
procedure NodeBarterRoom3;
procedure NodeBarterRoom4;
procedure NodeBarterRoom5;
procedure NodeBarterRoom6;
procedure NodeBarterRoom7;
procedure NodeBarterViski;
procedure NodeBarterViski2;
procedure NodeBarterViski3;
procedure NodeBarterViski4;
procedure NodeBarterMany;
procedure NodeBarterMany1;
procedure NodeBarterMany2;
procedure NodeBarterMany3;
procedure NodeBarterMany4;
procedure NodeBarterMany5;
procedure NodeBarterMany999;
procedure NodeBarterManyPrev;
procedure NodeBarterManyNo;
procedure NodeBarterManyOk;
procedure NodeAnswer100;
procedure NodeAnswer101;
procedure NodeAnswer102;
procedure NodeGomer;

//---------------------

//#define LVAR_Flag_A                     (8) // отвечает за ...

import variable box_stop_use;

variable manyCoast;
variable manyAmnt;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       set_start_home
       set_BarterBoxProc(ONE_GAME_WEEK)
   end
end

procedure setBarterA begin
        check_restock_item(PID_M_RADAWAY, 5, 5, 100)
        check_restock_item(PID_M_RAD_X, 5, 5, 100)
        check_restock_item(PID_M_ANTIDOTE, 5, 5, 100)
        check_restock_item(PID_M_STIMPAK, 12, 12, 100)

        check_restock_item(PID_M_FRUIT, 10, 10, 100)
        check_restock_item(PID_M_NUKA_COLA, 5, 7, 100)
        check_restock_item(PID_M_MEX_BEER, 5, 7, 100)

        //- крышки
        check_restock_many(500,700)
end

procedure setBarterB begin
   //! пока пусто
   set_BarterDopusk;
end

procedure look_at_p_proc begin
   lookProcMsg
end

procedure description_p_proc begin
   dsrptProcMsg
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 зарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 зарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       timer_move_to_home
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(120,121)
   end
end

procedure critter_p_proc begin
   set_using_stop // привязка хозяина и объекта (удалить, если не нужно)
   set_critter_options(1,0) // условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,1,1,1) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
   effectRegeratHP
end

procedure damage_p_proc begin
   set_damage_dude
end

procedure destroy_p_proc begin
   kill_critter_opt
   kill_critter_box
end

procedure talk_p_proc begin
   setReaction(1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,75) // поправка к отношению за принадлежность к фракции
   if (map_var(MVAR_DIEGO_C_BAR_COMBAT) == 1) then begin
   floater(self_obj, 102, COLOR_MSG_RED);
   end
   else begin
   dialog_proc_barter(0,0,NodeStartDialog,-1,-1)
   end
end

procedure Node998 begin
   set_enemy_dude;
end

procedure Node999 begin
end

procedure NodeStartDialog begin
   if (bitDialog(bit_1) == 0) then begin
   call NodeFstDialog;
   end
   else begin
   call NodeSndDialog;
   end
end

//--- ПЕРВЫЙ ДИАЛОГ

procedure NodeFstDialog begin
   bitDialogExp(bit_1,0)
   incBarterMod(75);
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   end
   else begin
   incBarterMod(50);
   end
   call NodeStartDialog;
end

//--- ПОВТОРНЫЙ ДИАЛОГ

procedure NodeSndDialog begin
   Reply(210)

   if (getQuest(GVAR_DAYGLOW_QST_CARAVAN) == qStart) then begin
      if (gvar_bit(GVAR_DAYGLOW_STATUS_CARAVAN, bit_9) == 0) then begin
      if (map_var(MVAR_DIEGO_C_CARREGULATOR) == 2) then begin
      NAnswer(220,0,NodeAnswer100)
      end
      end
   end
   NAnswer(212,0,NodeBarter)
   if (item_amnt(dude_obj,PID_MANY) > 0) then begin
   NAnswer(260,0,NodeBarterMany)
   end

   NAnswerRep(280,0,NodeBarterRoom,-30,280,NodeBarterRoom5)

   if (getQuest(GVAR_DAYGLOW_CONTRACT_VISKI) >= qStart) then begin
      if (item_amnt(dude_obj,PID_M_VISKI)>0) then begin
      GAnswer(240,0,NodeBarterViski3)
      end
   end
   if (getQuest(GVAR_DAYGLOW_CONTRACT_VISKI) == qZero) then begin
      NAnswer(230,0,NodeBarterViski)
   end

   if (bitDialog(bit_2) == 0) then begin
      NAnswer(226,0,NodeAnswer102)
   end

   if (global_var(GVAR_DAYGLOW_QST_GOMER) <= 2) then begin
      //NAnswer(241,0,NodeGomer)
   end
   NAnswer(211,0,Node999)
end

procedure NodeBarter begin
   goBarterMod;
   Reply(210)
   NAnswer(212,0,NodeBarter)
   NAnswer(211,0,Node999)
end

//--- СНЯТИЕ КОМНАТЫ

procedure NodeBarterRoom begin
   if (DudePerkHas(PERK_ghouling_fiziology) and town_rep_is_like(-10)) then begin
   manyCoast := 200;
   end
   else if town_rep_is_liked then begin
   manyCoast := 300;
   end
   else begin
   manyCoast := 400;
   end

   if town_rep_is_hated then begin
   manyCoast += manyCoast;
   end

   if DudePerkHas(PERK_ghouling_fiziology) then begin
   Reply(mstr(281)+manyCoast+mstr(282))
   end
   else begin
   Reply(mstr(281)+manyCoast+mstr(282))
   //NAnswer(283,0,NodeBarterRoom2)
   end
   NAnswer(287,0,NodeBarterRoom4)
   NAnswer(288,0,Node999)
end

procedure NodeBarterRoom2 begin
   zReply(284)
   BAnswer(285,0,NodeBarterRoom3)
   NAnswer(287,0,NodeBarterRoom4)
   NAnswer(288,0,Node999)
end

procedure NodeBarterRoom3 begin
   Reply(286)
   NAnswer(287,0,NodeBarterRoom4)
   NAnswer(288,0,Node999)
end

procedure NodeBarterRoom4 begin
   if (item_amnt(dude_obj,PID_BOOTLE_CAPS) >= manyCoast) then begin
   call NodeBarterRoom7;
   end
   else begin
   Reply(295)
   NAnswer(296,0,Node999)
   end
end

procedure NodeBarterRoom5 begin
   Reply(289)
   NAnswer(290,0,NodeBarterRoom6)
   NAnswer(291,0,Node999)
end

procedure NodeBarterRoom6 begin
   Reply(292)
   BAnswer(293,0,Node999)
   NAnswer(294,0,Node999)
end

procedure NodeBarterRoom7 begin
   item_remove(dude_obj,PID_BOOTLE_CAPS,manyCoast)
   item_add(self_obj,PID_BOOTLE_CAPS,manyCoast)
   move_to(dude_obj, 15118, 0);
   PLAY_FADE
   critter_uninjure(dude_obj, (DAM_BLIND bwor DAM_CRIP_LEG_LEFT bwor DAM_CRIP_LEG_RIGHT bwor DAM_CRIP_ARM_LEFT bwor DAM_CRIP_ARM_RIGHT));
   poison(dude_obj,-100);
   radiation_dec(dude_obj,10);
   critter_heal(dude_obj,25);
   game_time_advance(ONE_GAME_HOUR*12);
   call Node999;
end

//--- КОНТРАКТ НА ВИСКИ

procedure NodeBarterViski begin
   Reply(231)
   NAnswer(233,0,NodeBarterViski2)
   NAnswer(232,0,NodeSndDialog)
end

procedure NodeBarterViski2 begin
   setQuest(GVAR_DAYGLOW_CONTRACT_VISKI, qStart);
   Reply(234)
   if (item_amnt(dude_obj,PID_M_VISKI)>0) then begin
   GAnswer(236,0,NodeBarterViski3)
   end
   NAnswer(235,0,Node999)
end

procedure NodeBarterViski3 begin
   if (item_amnt(dude_obj,PID_M_VISKI)>0) then begin
   call NodeBarterViski4;
   end
   else begin
   call NodeBarterViski2;
   end
end

procedure NodeBarterViski4 begin
   inc_global_var(GVAR_DAYGLOW_CONTRACT_VISKI);
   exp_add(200);
   item_remove(dude_obj,PID_M_VISKI,1)
   item_add(dude_obj, PID_BOOTLE_CAPS, (proto_data(PID_M_VISKI, it_cost)*3))
   Reply(mstr(237)+(proto_data(PID_M_VISKI, it_cost)*3)+mstr(238))
   NAnswer(239,0,Node999)
end

//--- ДЕНЬГИ НА КРЫШКИ

procedure NodeBarterMany begin
   manyAmnt := 0;
   manyCoast := 10;
   if DudePerkHas(PERK_ghouling_fiziology) then begin
   manyCoast := 16;
   end
   if town_rep_is_antipaty then begin
   manyCoast -= 2;
   end
   if town_rep_is_hated then begin
   manyCoast := manyCoast/2;
   end

   if (getQuest(GVAR_DAYGLOW_MAIN_D) == 100) then begin
   manyCoast -= manyCoast/2;
   end
   else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 102) then begin
   manyCoast *= 2;
   end
   else if (getQuest(GVAR_DAYGLOW_MAIN_D) == 104) then begin
   manyCoast -= manyCoast/2;
   end

   if (manyCoast <= 0) then begin
   manyCoast := 1;
   end

   Reply(mstr(261)+manyCoast+mstr(262))

   NAnswer(265,0,NodeBarterMany1)
   if (item_amnt(dude_obj,PID_MANY) >= 2) then begin
   NAnswer(266,0,NodeBarterMany2)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 3) then begin
   NAnswer(267,0,NodeBarterMany3)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 4) then begin
   NAnswer(268,0,NodeBarterMany4)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 5) then begin
   NAnswer(269,0,NodeBarterMany5)
   end
   if (item_amnt(dude_obj,PID_MANY) >= 10) then begin
   //NAnswer(270,0,NodeBarterMany10)
   end
   NAnswer(271,0,NodeBarterMany999)

   NAnswer(264,0,NodeBarter)
   NAnswer(263,0,Node999)
end

procedure NodeBarterMany1 begin
   manyAmnt := 1;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany2 begin
   manyAmnt := 2;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany3 begin
   manyAmnt := 3;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany4 begin
   manyAmnt := 4;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany5 begin
   manyAmnt := 5;
   call NodeBarterManyPrev;
end

procedure NodeBarterMany999 begin
   manyAmnt := item_amnt(dude_obj,PID_MANY);
   call NodeBarterManyPrev;
end

procedure NodeBarterManyPrev begin
   if (manyAmnt <= 0) then begin
   call NodeBarterManyNo;
   end
   else if (item_amnt(dude_obj,PID_MANY) >= manyAmnt) then begin
   call NodeBarterManyOk;
   end
   else begin
   call NodeBarterManyNo;
   end
end

procedure NodeBarterManyNo begin
   Reply(272)
   NAnswer(273,0,Node999)
end

procedure NodeBarterManyOk begin
   item_add(dude_obj,PID_BOOTLE_CAPS,manyAmnt*manyCoast)
   item_remove(dude_obj,PID_MANY,manyAmnt)
   Reply(274)
   NAnswer(275,0,NodeSndDialog)
end

//--- РАССПРОСЫ

procedure NodeAnswer100 begin
   set_map_var(MVAR_DIEGO_C_CARREGULATOR, 3);
   Reply(221)
   NAnswer(222,0,NodeAnswer101)
end

procedure NodeAnswer101 begin
   Reply(223)
   NAnswer(224,0,Node999)
   BAnswer(225,0,Node999)
end

procedure NodeAnswer102 begin
   bitDialogExp(bit_2,0)
   Reply(227)
   NAnswer(228,0,NodeSndDialog)
end

procedure NodeGomer begin
   set_global_var(GVAR_DAYGLOW_QST_GOMER, 2);
   Reply(242)
   NAnswer(243,0,NodeSndDialog)
end
