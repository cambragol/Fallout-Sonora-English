#include "..\headers\define.h"
#include "..\headers\dayglow.h"
//---------------------

#define NAME                            SCRIPT_YCCrvMtn
#define TOWN_REP_VAR                    -1  // привязка к репутации (-1, если не нужно)

#define NUM_TEAM                        TEAM_MOTEL_MUTANT     // группа, к которой принадлежит персонаж (-1 если нужно оставить Team, заданный в редакторе)
#define NUM_AI                          AI_SUPERMUTANT        // AI персонажа (-1 если нужно оставить AI, заданный в редакторе)
#define KILL_SLAVA                      SLAVA_KILL_BAD        // слава за убийство этого персонажа (SLAVA_KILL_GOOD, SLAVA_KILL_BAD, SLAVA_KILL_NEITRAL, SLAVA_KILL_CHILD)
#define KILL_TOWN_REP                   REP_KILL_BAD          // репутация за убийство этого персонажа (REP_KILL_GOOD, REP_KILL_BAD, REP_KILL_NEITRAL, REP_KILL_CRITICAL для враждебности всего поселения, REP_KILL_CHILD)
#define KILL_FRAC_REP                   FRAC_NON              // принадлежность определенной фракции (-1, если не нужно)

#define FLAG_STEAL                      90 // Нижний порог навыка для кражи (для торговцев 200, для животных 300)
#define FLAG_ALCOHOL                    1  // Примет ли алкоголь от ГГ: 0 - принимает, 1 - не принимает
#define FLAG_FOOD                       1  // Примет ли пищу от ГГ: 0 - принимает, 1 - не принимает

//---------------------

procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure timed_event_p_proc;
procedure critter_p_proc;
procedure use_obj_on_p_proc;
procedure use_skill_on_p_proc;
procedure pickup_p_proc;
procedure combat_p_proc;
procedure damage_p_proc;
procedure destroy_p_proc;
procedure talk_p_proc;

procedure Node998;
procedure Node999;
procedure NodeStartDialog;
procedure NodeDialog;
procedure NodeDialog2;
procedure NodeDialog3;
procedure NodeDialog4;
procedure NodeDialog5;
procedure NodeDialog6;

//---------------------

#define LVAR_Tile_B                     (6) // номер второго тайла для патрулирования

import variable obj_critter_a; // указатель на караванщиков во время их побега

variable attakParam;

//---------------------



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
       if (map_first_run == true) then begin
       inc_map_var(MVAR_DIEGO_C_MUTANT_LIFE);
       end
       set_local_var(LVAR_Flags,0);
       set_local_var(LVAR_Self_Flags,0);
       set_start_home

       if (item_amnt(self_obj,PID_TEST) > 0) then begin
           if (cur_map_index == MAP_DAYGLOW_CAMP_OLD_TOWN) then begin
              if (item_amnt(self_obj,PID_TEST) == 1) then begin
              //set_local_var(LVAR_Tile_B,11111); //
              end
              if (item_amnt(self_obj,PID_TEST) == 2) then begin
              //set_local_var(LVAR_Tile_B,11111); //
              end
              if (item_amnt(self_obj,PID_TEST) == 3) then begin
              //set_local_var(LVAR_Tile_B,11111); //
              end
              if (item_amnt(self_obj,PID_TEST) == 4) then begin
              //set_local_var(LVAR_Tile_B,11111); //
              end
           end
           item_remove(self_obj,PID_TEST,999)
       end

       set_obj_visibility(self_obj, 1);
       if ((map_var(MVAR_DIEGO_C_MUTANT_LIFE) <= -100) or (getQuest(GVAR_DAYGLOW_MAIN_C) >= qEnd) or (global_var(GVAR_DAYGLOW_STATUS_RUTGER) > 0)) then begin
       destroy_object(self_obj);
       end
   end
end

procedure map_update_p_proc begin
   if (map_var(MVAR_DIEGO_C_MUTANT_LIFE) <= -100) then begin
       set_obj_visibility(self_obj, 1);
   end
   else if (getQuest(GVAR_DAYGLOW_MAIN_C) >= qEnd) then begin
       set_obj_visibility(self_obj, 1);
   end
   else if (map_var(MVAR_DIEGO_C_MUTANT_LIFE) > 0) then begin
       if (global_var(GVAR_DAYGLOW_STATUS_RUTGER) == 0) then begin
       if (getQuest(GVAR_DAYGLOW_MAIN_C) == qStart) then begin
       set_obj_visibility(self_obj, 0);
       end
       end
   end
end

procedure timed_event_p_proc begin
   // Таймер за номером 1 рарезервирован под перемещение персонажа по локации
   // Таймер за номером 2 рарезервирован под всплывающие реплики
   if (fixed_param == 1) then begin
       if (local_var(LVAR_Tile_B) > 0) then begin
       timer_move_patrol(HomeTile,local_var(LVAR_Tile_B))
       end
       else begin
       timer_move_random_home(5)
       end
   end
   if (fixed_param == 2) then begin
       //timer_float_reply(110,119)
   end
end

procedure critter_p_proc begin
   attakParam := 2;
   if (map_var(MVAR_DIEGO_C_MUTANT_LIFE) <=-100) then begin
   attakParam := 0;
   end
   else if ((lvar_bit(LVAR_Self_Flags, bit_15) != 0) and (lvar_bit(LVAR_Self_Flags, enemy_bit) == 0)) then begin
   attakParam := 0;
   end
   else if ((partyPidMtn != 0) and (lvar_bit(LVAR_Self_Flags, enemy_bit) == 0)) then begin
   attakParam := 0;
   end
   set_critter_options(attakParam,0) //условие нападения (0 личная вражда, 1 вражда клановая, 2 всегда), разворот к ГГ

   if (attakParam == 0) then begin
      if (obj_can_see_obj(self_obj,dude_obj) == true) then begin
      if ((map_var(MVAR_DIEGO_C_MUTANT_LIFE) >0) and (partyPidMtn != 0) and (lvar_bit(LVAR_Self_Flags, enemy_bit) == 0) and (lvar_bit(LVAR_Self_Flags, bit_15) == 0)) then begin
      dialogue_system_forcing(10)
      end
      end
   end

   if (self_visible == true) then begin
       if (map_var(MVAR_DIEGO_C_MUTANT_LIFE) >0) then begin
          if (obj_critter_a > 0) then begin
          if (obj_can_see_obj(self_obj,obj_critter_a) == true) then begin
             if (dude_is_sneaking and (tile_distance_objs(self_obj,obj_critter_a) > self_perception)) then begin
             end
             else begin
             attack(obj_critter_a);
             end
          end
          end
       end
   end
end

procedure use_skill_on_p_proc begin
   skillUseSteal
   skillUseFAid
end

procedure pickup_p_proc begin
   pickupProcCritter
end

procedure use_obj_on_p_proc begin
   UsedItemToCrit_Proc
end

procedure combat_p_proc begin
   effectAttak(0,0,3,3,3) //отравление min, облучение min, травма ног %, травма рук %, травма глаз %
end

procedure damage_p_proc begin
   if (source_obj == dude_obj) then begin
   GomerAttakDude
   end
   set_damage_dude
end

procedure destroy_p_proc begin
   dec_map_var(MVAR_DIEGO_C_MUTANT_LIFE);
   kill_critter_opt
end

procedure talk_p_proc begin
   if (map_var(MVAR_DIEGO_C_MUTANT_LIFE) <0) then begin
   floater(self_obj, 212, COLOR_MSG_RED);
   end
   else if (lvar_bit(LVAR_Self_Flags, bit_15) != 0) then begin
   floater(self_obj, 212, COLOR_MSG_RED);
   end
   else if ((map_var(MVAR_DIEGO_C_MUTANT_LIFE) >=0) and (partyPidMtn != 0) and (lvar_bit(LVAR_Self_Flags, enemy_bit) == 0)) then begin
   setReaction(-1) // отношение к ГГ (персонаж: 1 положительный, 0 нейтральный, -1 отрицательный)
   fracReaction(GVAR_TITUL_REBIRTH,25) // Поправка к отношению за принадлежность к фракциям
   dialog_proc(0,0,NodeStartDialog,-1,-1)
   end
   else begin
   floater(self_obj, 213, COLOR_MSG_RED);
   end
end

procedure Node998 begin
   GomerAttakDude
   setNodeCombat;
end

procedure Node999 begin
   if (map_var(MVAR_DIEGO_C_MUTANT_LIFE) <= -100) then begin
   PLAY_FADE
   end
end

procedure NodeStartDialog begin
   call NodeDialog;
end

procedure NodeDialog begin
   Reply(200)
   BAnswer(201,0,NodeDialog2)
   GAnswer(204,0,NodeDialog3)
end

procedure NodeDialog2 begin
   Reply(202)
   BAnswer(203,0,Node998)
end

procedure NodeDialog3 begin
   Reply(205)
   BAnswer(206,0,NodeDialog4)
   BAnswer(209,0,NodeDialog5)
   GAnswer(214,0,NodeDialog6)
end

procedure NodeDialog4 begin
   Reply(207)
   BAnswer(208,0,Node998)
end

procedure NodeDialog5 begin
   Reply(210)
   BAnswer(211,0,Node998)
end

procedure NodeDialog6 begin
   //set_map_var(MVAR_DIEGO_C_MUTANT_LIFE,-100);
   set_lvar_bit_on(LVAR_Self_Flags, bit_15);
   //Отсоединение кучи до конца квеста
   call Node999;
   //Reply(210)
   //NAnswer(211,0,Node999)
end
