#include "..\headers\define.h"
#include "..\headers\dayglow.h"
#define NAME                    SCRIPT_YSTrgmat

procedure start;
procedure map_enter_p_proc;
procedure set_type_proc;
procedure use_p_proc;
procedure use_skill_on_p_proc;
procedure use_obj_on_p_proc;
procedure unlock_proc;

#define LVAR_Open                       (0) // Сломан ли замок
#define LVAR_Lockpick                   (1) // Были ли использованы отмычки: 0 - нет, >0 - да

variable AutomatType; // тип товара: 0 - пиво, 1 - батарейки, 2 - рад-Х, 3 - кофе, 4 - машинное масло



procedure start begin
end

procedure map_enter_p_proc begin
   if (is_loading_game == false) then begin
      if (map_first_run) then begin
         call set_type_proc;
         if (AutomatType == 0) then begin
            item_add(self_obj,PID_M_MEX_BEER,5)
            if DudePerkHas(PERK_marauder) then begin
            item_add(self_obj,PID_M_MEX_BEER,2)
            end
         end
         if (AutomatType == 1) then begin
            item_add(self_obj,PID_AMMO_MICRO_FUSION_CELL,20)
            if DudePerkHas(PERK_marauder) then begin
            item_add(self_obj,PID_AMMO_MICRO_FUSION_CELL,10)
            end
         end
         if (AutomatType == 2) then begin
            item_add(self_obj,PID_M_RAD_X,5)
            if DudePerkHas(PERK_marauder) then begin
            item_add(self_obj,PID_M_RAD_X,2)
            end
         end
         if (AutomatType == 3) then begin
            item_add(self_obj,PID_COFFEE,1)
         end
         if (AutomatType == 4) then begin
            item_add(self_obj,PID_OIL,5)
            if DudePerkHas(PERK_marauder) then begin
            item_add(self_obj,PID_OIL,2)
            end
         end
      end
   end
end

procedure set_type_proc begin
   AutomatType := 0;
   if (self_PID == 474) then begin
   AutomatType := 1;
   end
   if (self_PID == 477) then begin
   AutomatType := 2;
   end
   if (self_PID == 478) then begin
   AutomatType := 0;
   end
   if (self_PID == 479) then begin
   AutomatType := 4;
   end
end

procedure use_p_proc begin
   if (local_var(LVAR_Open) == 0) then begin
       script_overrides;

       reg_anim_begin();
       reg_anim_clear(self_obj);
       reg_anim_clear(source_obj);
       reg_anim_end();

       display_mstr(110);
   end
end

procedure use_skill_on_p_proc begin
   variable skillcheck;
   skillcheck := 80;
   if (AutomatType == 0) then begin
   skillcheck := 40;
   end
   if (AutomatType == 1) then begin
   skillcheck := 80;
   end
   if (AutomatType == 2) then begin
   skillcheck := 80;
   end
   if (AutomatType == 3) then begin
   skillcheck := 70;
   end
   if (AutomatType == 4) then begin
   skillcheck := 70;
   end
   if (local_var(LVAR_Lockpick) != 0) then begin
   skillcheck -= local_var(LVAR_Lockpick);
   end

   if (action_being_used == SKILL_STEAL) then begin
      if (local_var(LVAR_Open) == 0) then begin
         obj_lock(self_obj);
      end

      if SELF_IS_LOCKED then begin
         script_overrides;
         display_mstr(110);
      end
   end

   if (action_being_used == SKILL_LOCKPICK) then begin
      script_overrides;

      if (local_var(LVAR_Open) > 0) then begin
         dude_floater_NON;
         display_mstr(111);
      end
      else if (has_skill(dude_obj, SKILL_LOCKPICK) < skillcheck) then begin
         skill_display_mstr(113,SKILL_LOCKPICK,skillcheck)
      end
      else begin
         item_add(self_obj,PID_MANY,1)
         call unlock_proc;
      end
   end
end

procedure use_obj_on_p_proc begin
   variable itemPID;
   variable itemAmnt;

   stopUseCarGaz

   else if (objUsedPid == PID_TOOL_LOCKPICKS_MH) then begin
      script_overrides;
      if (local_var(LVAR_Lockpick) == 0) then begin
         inc_local_var_amt(LVAR_Lockpick,20);
         play_sfx("WOR1XXX1");
         display_mstr(114);
         item_remove(dude_obj, objUsedPid, 1)
      end
      else begin
         display_mstr(115);
      end
   end

   else if (objUsedPid == PID_TOOL_LOCKPICKS_EL) then begin
      script_overrides;
      dude_floater_NON;
      display_mstr(116);
   end

   else if (objUsedPid == PID_MANY) then begin
      script_overrides;
      itemPID := 0;
      itemAmnt := 1;

      if (item_amnt(self_obj,PID_M_MEX_BEER) >0) then begin
      itemPID := PID_M_MEX_BEER;
      end
      else if (item_amnt(self_obj,PID_AMMO_MICRO_FUSION_CELL) >0) then begin
      itemPID := PID_AMMO_MICRO_FUSION_CELL;
      itemAmnt := 10;
      end
      else if (item_amnt(self_obj,PID_M_RAD_X) >0) then begin
      itemPID := PID_M_RAD_X;
      end
      else if (item_amnt(self_obj,PID_COFFEE) >0) then begin
      itemPID := PID_COFFEE;
      end
      else if (item_amnt(self_obj,PID_OIL) >0) then begin
      itemPID := PID_OIL;
      end

      if (local_var(LVAR_Lockpick) != 0) then begin
         display_mstr(111);
      end
      else if (itemPID >0) then begin
         item_add(self_obj,objUsedPid,1)
         item_remove(dude_obj, objUsedPid, 1)
         item_add(dude_obj,itemPID,itemAmnt)
         item_remove(self_obj, itemPID, itemAmnt)
         play_sfx("BEER");
         display_msg(mstr(119)+proto_data(itemPID, it_name)+", "+itemAmnt+mstr(121));
      end
      else begin
         dude_floater_NON;
         display_mstr(120);
      end
   end
end

procedure unlock_proc begin
   set_local_var(LVAR_Open,1);
   obj_unlock(self_obj);
   play_sfx("WOR1XXX1");
   exp_add(75);
   dude_floater_OK;
   display_mstr(117);
end
